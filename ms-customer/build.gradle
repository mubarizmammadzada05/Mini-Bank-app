plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'az.kb'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M4-groovy-4.0'
    testImplementation 'org.apache.groovy:groovy:4.0.22'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    test {
        groovy {
            srcDirs = ['src/test/groovy']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

compileTestGroovy {
    options.compilerArgs += ['--enable-preview']
    groovyOptions.javaAnnotationProcessing = true
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'az/kb/mscustomer/model/**',
                    'az/kb/mscustomer/config/**',
                    'az/kb/mscustomer/constant/**',
                    'az/kb/mscustomer/entity/**',
                    'az/kb/mscustomer/enums/**',
                    'az/kb/mscustomer/exception/**',
                    'az/kb/mscustomer/repository/**',
                    '**/MsCustomerApplication.class'
            ])
        }))
    }
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs = [
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    ]
}